# SPDX-License-Identifier: GPL-3.0-or-later
# Copyright (C) 2026 virtualgrub39
# Just for reference - not using any bloated generators in this project.

<song>          ::= <track> ( ";" <track> )* ";"?
<track>         ::= ( <action> | <definition> )*
<action>        ::= <note> | <command> | <loop> | <expansion>
<note>          ::= <pitch> <accidental>? <number>? <dots>?
                  | "r" <number>? <dots>?
<pitch>         ::= ['a'...'g']
<accidental>    ::= "+" | "-"
<dots>          ::= "."+
<command>       ::= [o|<|>|l|v|t] <number>?
<loop>          ::= "[" <action>* ":" <action>* "]" <number>
<definition>    ::= "!" <identifier> "{" <action>* "}"
<expansion>     ::= "@" <identifier>
